plugins {
    id 'org.springframework.boot' version "$springBootVersion"
    id 'io.spring.dependency-management' version "$springDependencyManagement"
    id 'java'
    id 'jacoco'
//    id 'io.gatling.gradle' version "$gatlingVersion"
//    id 'org.cyclonedx.bom' version "$cyclonedxVersion"
}

repositories {
    mavenCentral()
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'jacoco'
    apply plugin: 'io.spring.dependency-management'

    compileJava.options.encoding = 'UTF-8'
    compileTestJava.options.encoding = 'UTF-8'
    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(23)
        }
    }

    repositories {
        mavenCentral()
    }

    configurations {
        compileOnly.extendsFrom annotationProcessor
        testCompileOnly.extendsFrom annotationProcessor
    }

    dependencies {
        compileOnly 'org.projectlombok:lombok'
        annotationProcessor 'org.projectlombok:lombok'
        testCompileOnly 'org.projectlombok:lombok'
        testAnnotationProcessor 'org.projectlombok:lombok'

        testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    }

    dependencyManagement {
        imports {
            mavenBom org.springframework.boot.gradle.plugin.SpringBootPlugin.BOM_COORDINATES
            mavenBom "org.springframework.cloud:spring-cloud-dependencies:$springCloudVersion"
            mavenBom "io.zonky.test.postgres:embedded-postgres-binaries-bom:$zonkyPostgresBomVersion"
            mavenBom "io.grpc:grpc-bom:$grpcVersion"
        }
    }

    // tests configuration
    tasks.named('test') {
        useJUnitPlatform()
        finalizedBy jacocoTestReport
    }
    tasks.named('jacocoTestReport') {
        dependsOn test
    }
    jacoco {
        toolVersion = "$jacocoVersion"
    }
}

tasks.register('buildAll') {
    group = 'build'
    dependsOn ':app:build'
    dependsOn ':config:build'
    dependsOn ':inttest:it:build'
    dependsOn ':istiolab:backend:build'
    dependsOn ':istiolab:gateway:build'
    dependsOn ':loadtest:loaded-app:build'
}
